
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЭтоГруппа Тогда
		ВидПутевогоЛистаПоУмолчанию = Перечисления.УТЗ_ПЛ_ВидыПутевыхЛистов.Легковой;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	// Проверка на дубликаты наименований в справочнике (Сине-Никольский А.А.)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	УТЗ_ПЛ_Автотранспорт.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УТЗ_ПЛ_Автотранспорт КАК УТЗ_ПЛ_Автотранспорт
	|ГДЕ
	|	УТЗ_ПЛ_Автотранспорт.Ссылка <> &Ссылка
	|	И УТЗ_ПЛ_Автотранспорт.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		ОбщегоНазначения.СообщитьПользователю("Автотранспорт с таким наименованием уже есть в базе", Ссылка, "Наименование");
		Отказ = Истина;
	КонецЕсли;
	
	// Проверка на дубликаты в табличной части 
	ИсходныйМассив = СписокВодителей.Выгрузить(, "Водитель").ВыгрузитьКолонку("Водитель");
	МассивБезДубликатов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИсходныйМассив);
	
	Если ИсходныйМассив.Количество() > МассивБезДубликатов.Количество() Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("В табличной части есть повторяющиеся значения");
	КонецЕсли;
	
	// Проверка на заполнение реквизита "Топливо" для видов автотранспорта "Основной" и "С топливным баком"
	Если ВидАвтотранспорта = Перечисления.УТЗ_ПЛ_ВидыАвтотранспорта.ОсновнойТранспорт 
		Или ВидАвтотранспорта = Перечисления.УТЗ_ПЛ_ВидыАвтотранспорта.ПрицепыОборудованиеСТопливнымБаком Тогда
		
		Если Не ЗначениеЗаполнено(Топливо) Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Для указанного вида автотранспорта необходимо указать используемое топливо");
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
