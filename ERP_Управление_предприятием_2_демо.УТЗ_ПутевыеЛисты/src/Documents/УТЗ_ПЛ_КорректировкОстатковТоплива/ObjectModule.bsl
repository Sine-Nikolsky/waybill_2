#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.УТЗ_ПЛ_ОстаткиТоплива.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УТЗ_ПЛ_КорректировкОстатковТопливаСписокАвтотранспорта.Автотранспорт КАК Автотранспорт,
	|	УТЗ_ПЛ_КорректировкОстатковТопливаСписокАвтотранспорта.ВидДвижения КАК ВидДвижения,
	|	СУММА(УТЗ_ПЛ_КорректировкОстатковТопливаСписокАвтотранспорта.Количество) КАК Количество
	|ИЗ
	|	Документ.УТЗ_ПЛ_КорректировкОстатковТоплива.СписокАвтотранспорта КАК УТЗ_ПЛ_КорректировкОстатковТопливаСписокАвтотранспорта
	|ГДЕ
	|	УТЗ_ПЛ_КорректировкОстатковТопливаСписокАвтотранспорта.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	УТЗ_ПЛ_КорректировкОстатковТопливаСписокАвтотранспорта.Автотранспорт,
	|	УТЗ_ПЛ_КорректировкОстатковТопливаСписокАвтотранспорта.ВидДвижения";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.УТЗ_ПЛ_ОстаткиТоплива.Добавить();
		Если Выборка.ВидДвижения = Перечисления.УТЗ_ПЛ_ВидыДвиженийКорректировок.Приход Тогда
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата; // чтобы не было конфликтов при проведении 1 автомобиля по разным движениям
		ИначеЕсли Выборка.ВидДвижения = Перечисления.УТЗ_ПЛ_ВидыДвиженийКорректировок.Расход Тогда
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата + 1; // чтобы не было конфликтов при проведении 1 автомобиля по разным движениям
		КонецЕсли;
		
		Движение.Автотранспорт = Выборка.Автотранспорт;
		Движение.Количество = Выборка.Количество;
		Движение.Ответственный = Ответственный;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивАвтотранспорта = СписокАвтотранспорта.ВыгрузитьКолонку("Автотранспорт");
	МассивБезДубликатов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивАвтотранспорта);
	СписокАвтотранспортаСтрокой = СтрСоединить(МассивБезДубликатов, ", ");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли



