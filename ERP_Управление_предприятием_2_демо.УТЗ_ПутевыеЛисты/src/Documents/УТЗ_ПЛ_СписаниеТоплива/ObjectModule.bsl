

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = УТЗ_ПЛ_Организации.СсылкаОрганизацииУТЗ();
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.УТЗ_ПЛ_ТопливоКСписанию.Записывать = Истина;
	Движения.УТЗ_ПЛ_ТопливоКСписанию.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УТЗ_ПЛ_СписаниеТопливаТовары.ПутевойЛист КАК ДокументСписания,
	|	УТЗ_ПЛ_СписаниеТопливаТовары.Номенклатура КАК Номенклатура,
	|	УТЗ_ПЛ_СписаниеТопливаТовары.Ссылка.Подразделение КАК Подразделение,
	|	УТЗ_ПЛ_СписаниеТопливаТовары.Ссылка.Склад КАК Склад,
	|	СУММА(УТЗ_ПЛ_СписаниеТопливаТовары.Количество) КАК КоличествоКСписанию
	|ПОМЕСТИТЬ ВТДанныеТЧ
	|ИЗ
	|	Документ.УТЗ_ПЛ_СписаниеТоплива.Товары КАК УТЗ_ПЛ_СписаниеТопливаТовары
	|ГДЕ
	|	УТЗ_ПЛ_СписаниеТопливаТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	УТЗ_ПЛ_СписаниеТопливаТовары.ПутевойЛист,
	|	УТЗ_ПЛ_СписаниеТопливаТовары.Номенклатура,
	|	УТЗ_ПЛ_СписаниеТопливаТовары.Ссылка.Подразделение,
	|	УТЗ_ПЛ_СписаниеТопливаТовары.Ссылка.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеТЧ.ДокументСписания КАК ДокументСписания,
	|	ВТДанныеТЧ.Номенклатура КАК Номенклатура,
	|	ВТДанныеТЧ.Подразделение КАК Подразделение,
	|	ВТДанныеТЧ.Склад КАК Склад,
	|	ВТДанныеТЧ.КоличествоКСписанию КАК КоличествоКСписанию,
	|	ЕСТЬNULL(УТЗ_ПЛ_ТопливоКСписаниюОстатки.КоличествоКСписаниюОстаток, 0) КАК ОстатокТоплива
	|ИЗ
	|	ВТДанныеТЧ КАК ВТДанныеТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.УТЗ_ПЛ_ТопливоКСписанию.Остатки(
	|				&ДатаАктуальности,
	|				Номенклатура В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ВТДанныеТЧ.Номенклатура КАК Номенклатура
	|					ИЗ
	|						ВТДанныеТЧ КАК ВТДанныеТЧ)) КАК УТЗ_ПЛ_ТопливоКСписаниюОстатки
	|		ПО ВТДанныеТЧ.Номенклатура = УТЗ_ПЛ_ТопливоКСписаниюОстатки.Номенклатура
	|			И ВТДанныеТЧ.Подразделение = УТЗ_ПЛ_ТопливоКСписаниюОстатки.Подразделение
	|			И ВТДанныеТЧ.Склад = УТЗ_ПЛ_ТопливоКСписаниюОстатки.Склад
	|			И ВТДанныеТЧ.ДокументСписания = УТЗ_ПЛ_ТопливоКСписаниюОстатки.ДокументСписания";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаАктуальности", Дата);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.УТЗ_ПЛ_ТопливоКСписанию");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоКСписанию > Выборка.ОстатокТоплива Тогда
			Отказ = Истина;
			ТекстСообщения = СтрШаблон("Недостаточно топлива для списания. Доступно к списанию %1, предлагается списать %2"
			, Выборка.ОстатокТоплива
			, Выборка.КоличествоКСписанию);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		Движение = Движения.УТЗ_ПЛ_ТопливоКСписанию.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = Дата;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
