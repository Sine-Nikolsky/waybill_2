
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ЭтоСобытиеОбновленияСписка(ИмяСобытия) Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокПутевойЛист" Тогда
		ОткрытьФормуПутевогоЛистаВыбраннойСтроки();
	Иначе
		ОткрытьФормуДокументаВыбраннойСтроки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЗаявкуНаГрузовойАвтомобиль(Команда)

	ИмяВызываемойФормы = ИмяФормыДокумента(Тип("ДокументСсылка.УТЗ_ПЛ_ЗаявкаНаГрузовойАвтотранспорт"));
	ОткрытьФорму(ИмяВызываемойФормы, Новый Структура, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаявкуНаЛегковойАвтомобиль(Команда)

	ИмяВызываемойФормы = ИмяФормыДокумента(Тип("ДокументСсылка.УТЗ_ПЛ_ЗаявкаНаЛегковойАвтотранспорт"));
	ОткрытьФорму(ИмяВызываемойФормы, Новый Структура, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНаОснованииПутевойЛист(Команда)
	СсылкаНаДокумент = СсылкаНаДокументВыбраннойСтроки();
	ПараметрыФормы = Новый Структура("Основание", СсылкаНаДокумент);
	ОткрытьФорму("Документ.УТЗ_ПЛ_ПутевойЛист.Форма.ФормаДокумента", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуУдаления(Команда)
	СписокДокументов = СписокВыбранныхДокументов();
	УстановитьПометкуУдаленияНаСервере(СписокДокументов);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	ОткрытьФормуДокументаВыбраннойСтроки();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПроведение(Команда)
	
	ВыбранныеДокументы = СписокВыбранныхДокументов();
	Для Каждого Документ Из ВыбранныеДокументы Цикл
		ЗаписатьДокумент(Документ, РежимЗаписиДокумента.ОтменаПроведения);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	
	ВыбранныеДокументы = СписокВыбранныхДокументов();
	Для Каждого Документ Из ВыбранныеДокументы Цикл
		ЗаписатьДокумент(Документ, РежимЗаписиДокумента.Проведение);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	СкопироватьДокумент(СсылкаНаДокументВыбраннойСтроки());
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПутевойЛистНаОсновании(Команда)
	
	Основание = СсылкаНаДокументВыбраннойСтроки();
	ПараметрыФормы = Новый Структура("Основание", Основание);
	ОткрытьФорму("Документ.УТЗ_ПЛ_ПутевойЛист.Форма.ФормаДокумента", ПараметрыФормы, ЭтотОбъект, Основание);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСТабличнойЧастьюСписок

&НаКлиенте
Функция СсылкаНаДокументВыбраннойСтроки()
	
	ТекДанн = Элементы.Список.ТекущиеДанные;
	Если ТекДанн = Неопределено Тогда
		Текст = НСтр("ru='Документ не выбран'", "ru");
		ВызватьИсключение Текст;
	КонецЕсли;
		
	Возврат ТекДанн.Ссылка;
	
КонецФункции

&НаКлиенте
Функция ТипДокументаВыбраннойСтроки()
	ТекДанн = Элементы.Список.ТекущиеДанные;
	Возврат ТекДанн.Тип;
КонецФункции

&НаКлиенте
Функция ИмяФормыДокумента(ТипДокумента)
	
	ИмяВызываемойФормы = "";
	
	Если ТипДокумента = Тип("ДокументСсылка.УТЗ_ПЛ_ЗаявкаНаГрузовойАвтотранспорт") Тогда
		ИмяВызываемойФормы = "Документ.УТЗ_ПЛ_ЗаявкаНаГрузовойАвтотранспорт.Форма.ФормаДокумента";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.УТЗ_ПЛ_ЗаявкаНаЛегковойАвтотранспорт") Тогда
		ИмяВызываемойФормы = "Документ.УТЗ_ПЛ_ЗаявкаНаЛегковойАвтотранспорт.Форма.ФормаДокумента";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяВызываемойФормы) Тогда
		Текст = НСтр("ru='Не определена необходимая форма для открытия документа.'", "ru");
		ВызватьИсключение Текст;
	КонецЕсли;
	
	Возврат ИмяВызываемойФормы;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуДокументаВыбраннойСтроки()
	
	СсылкаНаДокумент = СсылкаНаДокументВыбраннойСтроки();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", СсылкаНаДокумент);
	ИмяВызываемойФормы = "";
	
	ТипДокумента = ТипДокументаВыбраннойСтроки();
	
	ИмяВызываемойФормы = ИмяФормыДокумента(ТипДокумента);
	
	ОткрытьФорму(ИмяВызываемойФормы, ПараметрыФормы, ЭтотОбъект, СсылкаНаДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПутевогоЛистаВыбраннойСтроки()
	
	ТекДанн = Элементы.Список.ТекущиеДанные;
	Если ТекДанн = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекДанн.ПутевойЛист) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекДанн.ПутевойЛист);
		ОткрытьФорму("Документ.УТЗ_ПЛ_ПутевойЛист.Форма.ФормаДокумента", ПараметрыФормы, ЭтотОбъект, ТекДанн.ПутевойЛист);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СписокВыбранныхДокументов()

	ВыбранныеДокументы = Элементы.Список.ВыделенныеСтроки;
	
	Если ВыбранныеДокументы.Количество() = 0 Тогда
		Текст = НСтр("ru='Не выбран ни один документ'", "ru");
		ВызватьИсключение Текст;
	КонецЕсли;
	
	Возврат ВыбранныеДокументы;
	
КонецФункции

&НаКлиенте
Функция ЭтоЗаявкаНаЛегковойАвтотранспорт()
	
	Ответ = Ложь;
	
	Если ТипДокументаВыбраннойСтроки() = Тип("ДокументСсылка.УТЗ_ПЛ_ЗаявкаНаЛегковойАвтотранспорт") Тогда 
		Ответ = Истина;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

&НаКлиенте
Функция ЭтоЗаявкаНаГрузовойАвтотранспорт()
	
	Ответ = Ложь;
	
	Если ТипДокументаВыбраннойСтроки() = Тип("ДокументСсылка.УТЗ_ПЛ_ЗаявкаНаГрузовойАвтотранспорт") Тогда 
		Ответ = Истина;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииКонтекстногоМенюСписок

&НаКлиенте
Процедура ЗаписатьДокумент(СсылкаНаДокумент, РежимЗаписиДокумента)

	ЗаписатьДокументНаСервере(СсылкаНаДокумент, РежимЗаписиДокумента);
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьДокументНаСервере(СсылкаНаДокумент, РежимЗаписиДокумента)
	Ссылка = Документы.УТЗ_ПЛ_ЗаявкаНаГрузовойАвтотранспорт.НайтиПоНомеру("1");
	ДокументОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	ДокументОбъект.Записать(РежимЗаписиДокумента);
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьДокумент(СсылкаНаДокумент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначениеКопирования", СсылкаНаДокументВыбраннойСтроки());
	ИмяВызываемойФормы = ИмяФормыДокумента(ТипДокументаВыбраннойСтроки());
	
	ОткрытьФорму(ИмяВызываемойФормы, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПометкуУдаленияНаСервере(СписокДокументов) 
	
	Для Каждого Документ Из СписокДокументов Цикл
		
		ДокументОбъект = Документ.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(Не ДокументОбъект.ПометкаУдаления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Функция ЭтоСобытиеОбновленияСписка(ИмяСобытия)
	
	ОтслеживаемыеСобытия = Новый Массив;
	ОтслеживаемыеСобытия.Добавить("Запись_УТЗ_ПЛ_ЗаявкаНаЛегковойАвтомобиль");
	ОтслеживаемыеСобытия.Добавить("Запись_УТЗ_ПЛ_ЗаявкаНаГрузовойАвтомобиль");
	ОтслеживаемыеСобытия.Добавить("Запись_УТЗ_ПЛ_ПутевойЛист");
	
	ЭтоПодходящееСобытие = ОтслеживаемыеСобытия.Найти(ИмяСобытия) <> Неопределено;
	
	Возврат ЭтоПодходящееСобытие;
	
КонецФункции

#КонецОбласти

#КонецОбласти
