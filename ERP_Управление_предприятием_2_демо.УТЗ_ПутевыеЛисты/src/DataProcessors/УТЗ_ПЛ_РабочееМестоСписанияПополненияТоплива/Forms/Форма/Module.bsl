
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ВнутреннееПотребление" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	ОбновитьДанные();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументСписания(Команда)
	
	ТекДанн = Элементы.ТЗПодразделения.ТекущиеДанные;
	Если ТекДанн = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Подразделение не выбрано, создание документа отменено");
		Возврат;
	КонецЕсли;
	
	СозданныеДокументы = Неопределено;
	СоздатьДокументСписанияНаСервере(ТекДанн.Подразделение, СозданныеДокументы);
	
	Если ТипЗнч(СозданныеДокументы) = Тип("Массив") Тогда
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъектов(СозданныеДокументы);
		
		Для Каждого СозданныйДокумент Из СозданныеДокументы Цикл
			ПараметрыФормы = Новый Структура("Ключ", СозданныйДокумент);
			ОткрытьФорму("Документ.УТЗ_ПЛ_СписаниеТоплива.Форма.ФормаДокумента"
						, ПараметрыФормы
						, ЭтотОбъект);
		КонецЦикла;
					
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеТЗТопливоКСписанию(Команда)
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура ТопливоКСписаниюВыделитьВсе(Команда)
	ТекСтр = Элементы.ТЗПодразделения.ТекущиеДанные;
	Подразделение = ТекСтр.Подразделение;
	
	Для Каждого Строка Из ТЗТопливоКСписанию Цикл
		
		Если Строка.Подразделение = Подразделение Тогда
			Строка.Пометка = Истина;
		Иначе
			Строка.Пометка = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоКСписаниюСнятьВсеОтметки(Команда)
	Для Каждого Строка Из ТЗТопливоКСписанию Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанные()
	
	ТЗПодразделения.Очистить();
	ТЗТопливоКСписанию.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УТЗ_ПЛ_ТопливоКСписаниюОстатки.ДокументСписания КАК ДокументСписания,
	|	УТЗ_ПЛ_ТопливоКСписаниюОстатки.Номенклатура КАК Номенклатура,
	|	УТЗ_ПЛ_ТопливоКСписаниюОстатки.Подразделение КАК Подразделение,
	|	УТЗ_ПЛ_ТопливоКСписаниюОстатки.Склад КАК Склад,
	|	УТЗ_ПЛ_ТопливоКСписаниюОстатки.КоличествоКСписаниюОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.УТЗ_ПЛ_ТопливоКСписанию.Остатки(&ДатаАктуальности, ) КАК УТЗ_ПЛ_ТопливоКСписаниюОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение
	|ИТОГИ ПО
	|	Подразделение
	|АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр("ДатаАктуальности", КонецДня(ТекущаяДатаСеанса()));
	
	ВыборкаПодразделения = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПодразделения.Следующий() Цикл
		
		СтрокаПодразделение = ТЗПодразделения.Добавить();
		СтрокаПодразделение.Подразделение = ВыборкаПодразделения.Подразделение;
		
		ВыборкаДетальныеЗаписи = ВыборкаПодразделения.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Строка = ТЗТопливоКСписанию.Добавить();
			ЗаполнитьЗначенияСвойств(Строка, ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗПодразделенияПриАктивизацииСтроки(Элемент)
	
	ТекДанн = Элементы.ТЗПодразделения.ТекущиеДанные;
	
	Если ТекДанн = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Подразделение = ТекДанн.Подразделение;
	
	Элементы.ТЗТопливоКСписанию.ОтборСтрок = Новый ФиксированнаяСтруктура("Подразделение", Подразделение);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияНаСервере()
		
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументСписанияНаСервере(Подразделение, СозданныеДокументы = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеТЗ.ДокументСписания КАК ДокументСписания,
	|	ДанныеТЗ.Номенклатура КАК Номенклатура,
	|	ДанныеТЗ.Склад КАК Склад,
	|	ДанныеТЗ.Количество КАК Количество,
	|	ДанныеТЗ.Подразделение КАК Подразделение,
	|	ДанныеТЗ.Пометка КАК Пометка
	|ПОМЕСТИТЬ ВТДанныеТЗТопливоКСписанию
	|ИЗ
	|	&ДанныеТЗ КАК ДанныеТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеТЗТопливоКСписанию.ДокументСписания КАК ДокументСписания,
	|	ВТДанныеТЗТопливоКСписанию.Номенклатура КАК Номенклатура,
	|	ВТДанныеТЗТопливоКСписанию.Склад КАК Склад,
	|	ВТДанныеТЗТопливоКСписанию.Количество КАК Количество,
	|	ВТДанныеТЗТопливоКСписанию.Подразделение КАК Подразделение,
	|	ВТДанныеТЗТопливоКСписанию.Пометка КАК Пометка
	|ИЗ
	|	ВТДанныеТЗТопливоКСписанию КАК ВТДанныеТЗТопливоКСписанию
	|ГДЕ
	|	ВТДанныеТЗТопливоКСписанию.Подразделение = &Подразделение
	|	И ВТДанныеТЗТопливоКСписанию.Пометка
	|ИТОГИ ПО
	|	Склад";
	Запрос.УстановитьПараметр("ДанныеТЗ", РеквизитФормыВЗначение("ТЗТопливоКСписанию"));
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю("Не выбраны строки расхода топлива. Создание документа отменено");
		Возврат;
	КонецЕсли;
	
	ВыборкаСклады = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СозданныеДокументы = Новый Массив;
	
	Пока ВыборкаСклады.Следующий() Цикл
			
		НовыйДокумент = Документы.УТЗ_ПЛ_СписаниеТоплива.СоздатьДокумент();
		НовыйДокумент.Дата = ТекущаяДатаСеанса();
		НовыйДокумент.Организация = УТЗ_ПЛ_Организации.СсылкаОрганизацииУТЗ();
		НовыйДокумент.Подразделение = Подразделение;
		НовыйДокумент.Ответственный = Пользователи.ТекущийПользователь();
		НовыйДокумент.Склад = ВыборкаСклады.Склад;
		НовыйДокумент.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
		
		ВыборкаДетальныеЗаписи = ВыборкаСклады.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			НоваяСтрока = НовыйДокумент.Товары.Добавить();
			НоваяСтрока.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			НоваяСтрока.Количество = ВыборкаДетальныеЗаписи.Количество;
			НоваяСтрока.ПутевойЛист = ВыборкаДетальныеЗаписи.ДокументСписания;
			
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
		СозданныеДокументы.Добавить(НовыйДокумент.Ссылка);
		
		ТекстСообщения = СтрШаблон("Документ ""%1"" создан. Необходимо его проверить, исправить и, при необходимости, перепровести", НовыйДокумент.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, НовыйДокумент.Ссылка);
		
	КонецЦикла;

	ОбновитьДанные();
	
	
КонецПроцедуры






#КонецОбласти


